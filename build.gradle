plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.1"
}

group = 'me.dunescifye'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }

    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://maven.playpro.com'
    }
    maven {
        name = 'md_5-public'
        url = 'https://repo.md-5.net/content/groups/public/'
    }
    maven { url = 'https://jitpack.io' }

}
dependencies {
    compileOnly "net.kyori:adventure-api:4.17.0"
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    compileOnly "dev.jorel:commandapi-bukkit-core:9.4.0"
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'net.coreprotect:coreprotect:22.4'
    compileOnly 'LibsDisguises:LibsDisguises:10.0.44'
    compileOnly 'com.github.GriefPrevention:GriefPrevention:16.18.3'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly("de.tr7zw:item-nbt-api-plugin:2.13.1")
    implementation 'com.jeff-media:MorePersistentDataTypes:2.4.0'
    implementation 'com.jeff-media:custom-block-data:2.2.2'
}

shadowJar {
    relocate 'com.jeff_media.morepersistentdatatypes', 'me.dunescifye.libs.morepersistentdatatypes'
    relocate 'com.jeff_media.customblockdata', 'me.dunescifye.libs.customblockdata'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
